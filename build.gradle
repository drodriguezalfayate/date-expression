plugins {
    id 'java-library'
    id 'antlr'
    id 'maven-publish'
    id 'signing'
}

group 'com.proxiasuite.utils'
version '1.0.2'

// Por el plugin de java: sourceCompatibility, targetCompatibility, archivesBaseName ...
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    antlr "org.antlr:antlr4:4.13.1"
    api("org.antlr:antlr4-runtime:4.13.1")
}

configurations {
    api {
        // Undo extendsFrom relationship between the 'antlr' configuration and the 'api' configuration
        // See https://github.com/gradle/gradle/issues/820
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}


javadoc {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

afterEvaluate {
    tasks.findByName('sourcesJar').dependsOn(tasks.findByName('generateGrammarSource'))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'date-expression'
                description = 'Librería que permite analizar una expresión de fecha en lenguaje natural y convertirla a fechas reales del sistema en un periodo de tiempo'
                url = 'https://github.com/drodriguezalfayate/date-expression'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'drodriguezalfayate'
                        name = 'David Rodríguez Alfayate'
                        email = 'davidrodriguezalfayate@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:drodriguezalfayate/date-expression.git'
                    developerConnection = 'scm:git@github.com:drodriguezalfayate/date-expression.git'
                    url = 'https://github.com/drodriguezalfayate/date-expression.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}



test {
    useJUnitPlatform()
}
